plugins {
    id 'com.android.library'
    id 'de.marcphilipp.nexus-publish'
    id 'signing'
    id 'kotlin-android'
}
// Your Group
group='com.github.Citcon'

def LOCALHOST_IP = System.properties['LOCALHOST_IP'] ?: '"10.0.2.2"'
def GATEWAY_IP = System.properties['GATEWAY_IP'] ?: '"10.0.2.2"'
def GATEWAY_PORT = System.properties['GATEWAY_PORT'] ?: '"3000"'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion '30.0.3'

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName '1.0.1'

        consumerProguardFiles 'proguard.pro'

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }

    testOptions.unitTests {
        returnDefaultValues true
        includeAndroidResources true
    }

    lintOptions {
        warning 'GradleCompatible'
        textReport true
        textOutput 'stdout'
        disable 'InvalidPackage' // The Cardinal SDK is referencing the `javax.naming` package but it is unavailable
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
    }

    buildTypes.debug { type ->
        type.buildConfigField 'String', 'LOCALHOST_IP', LOCALHOST_IP
        type.buildConfigField 'String', 'GATEWAY_IP', GATEWAY_IP
        type.buildConfigField 'String', 'GATEWAY_PORT', GATEWAY_PORT
    }
}

dependencies {
    api ('com.braintreepayments.api:braintree:3.17.4'){transitive = true}
    //api project(':CardForm')
    api 'com.citcon.sdk:CardForm:1.0.1'
    api 'com.braintreepayments.api:three-d-secure:3.17.4'

    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.gms:play-services-wallet:19.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.6.1'

    testImplementation 'com.squareup.assertj:assertj-android:1.1.1'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.6'
    testImplementation 'org.powermock:powermock-module-junit4-rule:1.6.6'
    testImplementation 'org.powermock:powermock-api-mockito:1.6.6'
    testImplementation 'org.powermock:powermock-classloading-xstream:1.6.6'
    testImplementation 'org.robolectric:robolectric:4.5.1'
    testImplementation 'org.skyscreamer:jsonassert:1.4.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    testImplementation "androidx.core:core-ktx:1.8.0"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.21"
    testImplementation 'com.google.android.gms:play-services-wallet:19.1.0'

}

ext {
    PUBLISH_GROUP_ID = 'com.citcon.sdk'
    PUBLISH_VERSION = '1.0.1'
    PUBLISH_ARTIFACT_ID = 'drop-in'
}

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"

tasks.withType(Javadoc).all { enabled = false }

